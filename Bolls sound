/*
*This program uses Arduino to read the voltage quotients of two different "Yes" and "No" target buttons. 
*These values are then used to count number of times each target has effectively been hit, 
*trigger sound feedback, and advance through a series of predetermined questions.
*
*This program was created for by Bibiana Bauer in December of 2017 
*for the final project in the IxD Objects and Space course at California College of the Arts.
*/

//imports necessary libraries
import processing.sound.*;
import processing.serial.*;
import cc.arduino.*;

// initializes arduino
Arduino arduino;

//sets up array for sound files
SoundFile soundfile;
SoundFile[] files = new SoundFile[2];

// initiliazes list of strings to store question prompts
StringList questions; 
//initializes variables and values for question number and question index within the list of questions
int responseIndex = 0;
int questionNumber = 0;
// initializes variables and values for both the "Yes" and "No" buttons
int buttonY = 0;
int buttonN = 0;
//initializes variables and values for last recoreded value for each button
int buttonYLast = 0;
int buttonNLast = 0;
// initializes and sets initial values for the variables to count how many times each button has been pressed
int buttonYPresses[] = new int[15];
int buttonNPresses[] = new int[15];
//initialized boolean value for "Yes" and "No" buttons
boolean responseY = false;
boolean responseN = false;


void setup() {
  //set program to display at fullscreen
  fullScreen();

  //initializes string list for questions
  questions = new StringList();

  //adds questions to string list
  questions.append("1. Are you scared to stand up for what you believe in?");
  questions.append("2. Do you get high before class?");
  questions.append("3. Is a hot dog a sandwich?");
  questions.append("4. Do you wash your hands every time you visit the restroom?");
  questions.append("5. Are you ok?");
  questions.append("6. Are you happy with your major?");
  questions.append("7. Ever drink alcohol from a shoe?");
  questions.append("8. Do you see a therapist?");
  questions.append("9. Has your car been broken into while parked at CCA?");
  questions.append("10. Do you have a best friend at CCA?");
  questions.append("11. Do you call yourself an artist?");
  questions.append("12. Do you call yourself a designer??");
  questions.append("13. Have you filled out a teacher review and felt it made a difference?");
  questions.append("14. Are dogs better than cats?");
  questions.append("15. Do you drink boba tea?");


  //imports selected sound files from /data folder
  files[0] = new SoundFile(this, "cheers.mp3");
  files[1] = new SoundFile(this, "boos.mp3");
  files[0].stop();
  files[1].stop();

  // Prints out the available serial ports.
  printArray(Serial.list()); 

  // enter the index of the serial port number corresponding to the Arduino board in the [] brackets
  arduino = new Arduino(this, Arduino.list()[5], 57600);
}

void draw() {
  background(0);
  getButtonValues();
  displayQuestions();
  playAudio();
  drawTargets();
  //resets response booleans to false to prevents questions from automatically advancing 
  responseY = false; 
  responseN = false;
}

void getButtonValues() {
  //stores last value for "YES" and "NO" buttons
  buttonYLast = buttonY; 
  buttonNLast = buttonN;
  //gets current value for "Yes" and "No" buttons
  buttonY = arduino.analogRead(0);
  buttonN = arduino.analogRead(1);
  /*
  *thresh sets the threshhold for button readings
  *a lower number will make the targets MORE sensitive 
  *and high number will make them LESS sensitive
  */
  int thresh = 900;
  //checks to see if "Yes" target has been hit
  if (buttonY > thresh && buttonYLast <= thresh) {
    //changes reponse status for "Yes" button to true
    responseY = true;
    //increases "Yes" count for current question by 1
    buttonYPresses[responseIndex]++;
  }
  if (buttonN > thresh && buttonNLast <= thresh) {
    //changes reponse status for "No" button to true
    responseN = true;
    //increases "No" count for current question by 1
    buttonNPresses[responseIndex]++;
  }
}

void displayQuestions() {
  //sets font display settings
  fill(255);
  textSize(40);
  textAlign(CENTER);
  //chooses question from list
  String currentQuestion = questions.get(questionNumber);  
  //displays current question
  text(currentQuestion, 650, 120);  
  //checks to see if either one of the targets has been hit
  if (responseY == true || responseN == true) { 
    //advances to next question
    questionNumber++; 
    responseIndex++;
    //checks to see if end of questions list has been reached
    if (questionNumber == questions.size()) {
      //resets to the beginning of the questions list
      questionNumber = 0;
      responseIndex = 0;
    }
  }
}

void drawTargets() {
  noStroke();
  // sets fill color to GREEN
  fill(0, 255, 0);
  //draws circle based on current value for "YES" button
  ellipse(350, 400, buttonY/3, buttonY/3);
  // sets fill color to RED
  fill(255, 0, 0);
  //draws circle based on current value for "NO" button
  ellipse(920, 400, buttonN/3, buttonN/3);
}

void playAudio() {
  //checks to see if "Yes" button has been hit
  if (responseY == true) {
    //loads cheering soundfile and plays it back 
    files[0].jump(1);
    files[0].play();
  }
  //checks to see if "No" button been pressed
  if (responseN == true) {
    //loads booing soundfile and plays it back
    files[1].jump(1);
    files[1].play();
  }
}

void keyReleased() {
  /*
  *maps the 'y' and 'n' keys as substitutes to hitting 
  *the "Yes" and "No" targets mainly used for debugging and 
  * testing when the program is not hooked up to the arduino
  */
  if (key == 'y') {
    responseY = true;
    buttonYPresses[responseIndex]++;
  }
  if (key == 'n') {
    responseN = true;
    buttonNPresses[responseIndex]++;
  }
  //creates scorecard for all questions and the complete tally of responses
  if (key == 'p') {
    //prints out entire array of the count of "Yes" hits for each question in the list
    print("Y");
    System.out.println(java.util.Arrays.toString(buttonYPresses));
    //prints out entire array of the count of "No" hits for each question in the list
    print("N");
    System.out.println(java.util.Arrays.toString(buttonNPresses));
  }
}
